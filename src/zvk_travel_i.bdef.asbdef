managed implementation in class zbp_vk_travel_i unique;
strict ( 2 );
with draft; //To enable draft functionality for odataV4

//**************************************************Travel****************************************************************************
define behavior for ZVK_TRAVEL_I alias Travel
persistent table zvk_des_travel
with additional save   // FOR ADDITIONAL SAVE WITH MANAGED SCENARIO ONLY AND WE NEED TO WRITE SAVE MODIFIED SAVE SEQUENCE.
draft table zvk_d_travel //Draft table
//to create the above draft table Select the draft table name and right click then select Quickfix then it creates
lock master
total etag LastChangedAt //draft related{TOTAL BUSINESS OBJECT LEVEL}
authorization master ( global, instance )
etag master LocalLastChangedAt  //ENTITY LEVEL only
{
  create ( authorization : global );
  update;
  delete;
   field ( readonly ) TotalPrice;
  field ( numbering : managed, readonly ) TravelUuid;
    field ( readonly ) TravelId, OverallStatus;
//  field ( mandatory : create, readonly : update ) TravelId, OverallStatus; //WE WILL GET A POPUP TYPE WHILE CREATING NEW TRAVEL
  field ( features : instance ) BookingFee;

  association _Booking { create; with draft; }

  //draft related
  draft action Edit;
  draft action Discard;
  draft action Resume;
  draft action Activate optimized;
//   draft determine action Prepare;
  draft determine action Prepare
  {
    validation ValidateCustomer;
    validation ValidateAgency;
    validation validateDates;
  }
  //draft related


  //Determinations
  determination SetTravelID on save { create; update; }
  determination SetOverallstatus on modify { create; update; }
  determination calculateTotalPrice on modify { create; field BookingFee, CurrencyCode; } //IN THIS "ReCalcTotalPrice" CALLED LIKE A FM

//  //Validations
  validation ValidateCustomer on save { create; field CustomerId; }
  validation ValidateAgency on save { create; field AgencyId; }
  validation ValidateDates on save { create; field BeginDate, EndDate; }


  //Actions
  action ( features : instance ) AcceptTravel result [1] $self;
  action ( features : instance ) RejectTravel result [1] $self;
  action ( features : instance ) DeductDiscount parameter ZVK_A_DISCOUNT result [1] $self { default function GetDefaultsForDeductDiscount; }
  internal action ReCalcTotalPrice; //internal action is meant to be our BO's and these are not for USER SPECIFIC

  determine action ValidateCustomerId { validation ValidateCustomer; }
  determine action ValidateAgencyId { validation ValidateAgency; }
  determine action ValidateDatesVal { validation ValidateDates; }

  side effects
  { field BookingFee affects field TotalPrice;
    determine action ValidateCustomerId executed on field CustomerId affects messages;
    determine action ValidateAgencyId executed on field AgencyId affects messages;
    determine action ValidateDatesVal executed on field BeginDate, field EndDate affects messages; }


  mapping for zvk_des_travel
    {

      //cds entity aliyas name = database level field name
      TravelUuid         = travel_uuid;
      TravelId           = travel_id;
      AgencyId           = agency_id;
      CustomerId         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}
//*********************************************************************************************************************************************

//********************BOOKING****************************************************************************************************************

define behavior for ZVK_BOOKING_I alias Booking
persistent table zvk_des_booking
draft table zvk_d_booking  //draft related
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  // field ( readonly ) BookingUuid;
  field ( readonly ) TravelUuid, BookingId, BookingDate;
  field ( numbering : managed, readonly ) BookingUuid;
  association _Travel { with draft; } //draft related
  association _Bookingsupplement { create; with draft; } //draft related

  determination SetBookingID on save { create; }
  determination SetBookingDate on save { create; }

  determination calculateTotalPrice on modify { create; field FlightPrice, CurrencyCode; }
  side effects { field FlightPrice affects field _travel.TotalPrice; }



  mapping for zvk_des_booking
    {

      //cds entity aliyas name = database level field name
      BookingUuid        = booking_uuid;
      TravelUuid         = parent_uuid;
      BookingId          = booking_id;
      BookingDate        = booking_date;
      CustomerId         = customer_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      CurrencyCode       = currency_code;
      BookingStatus      = booking_status;
      LocalLastChangedAt = local_last_changed_at;
    }
}
//*********************************************************************************************************************************************

//********************BOOKING_SUPPLEMENT****************************************************************************************************************

define behavior for ZVK_BKSPL_I alias Bookingsuppl
persistent table zvk_des_bksuppl
draft table zvk_d_bksuppl  //draft related
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( readonly ) BookingUuid, TravelUuid, BookingSupplementId;
  field ( numbering : managed, readonly ) BooksupplUuid;
  association _Travel { with draft; } //draft related
  association _Booking { with draft; } //draft related

  determination SetBookingSupplId on save { create; }

  determination calculateTotalPrice on modify { create; field Price, CurrencyCode; }
  side effects { field Price affects field _Travel.TotalPrice; }

  mapping for zvk_des_bksuppl
    {
      BooksupplUuid       = booksuppl_uuid;
      TravelUuid          = root_uuid;
      BookingUuid         = parent_uuid;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LocalLastChangedAt  = local_last_changed_at;
    }

  //*********************************************************************************************************************************************

















}